// Подключение заголовочных файлов
#include <SPI.h>
#include <Ethernet.h>

// MAC-адрес контроллера (иногда указан на Ethernet Shield)
byte mac[] = { 0xDE, 0xAD, 0xBE, 0xEF, 0xFE, 0xED };
byte is_connected = 0;

// Адрес/доменное имя сервера и порт
byte srv_ip[] = {74,125,232,128}; // или char server_url = "www.google.com";
int srv_port = 80;

// Создание Ethernet-клиента
EthernetClient client;

void setup() {
  // Запуск COM-порта:
  Serial.begin(9600);
  // Ожидание запуска COM-порта
  while (!Serial) {
    delay(10);
  }

  // Запуск интернет-соединения:
  Serial.println("Initializing Ethernet");
  if (Ethernet.begin(mac) == 0) {
    Serial.println("Ethernet initialization failed");
    is_connected = 0;
  } else {
    // Вывод полученного IP-адреса при удачном запуске
    Serial.print("Successful Ethernet initialization, my IP: ");
    Serial.println(Ethernet.localIP());

    // Попытка подключения к серверу
    if (client.connect(srv_ip, srv_port)) {
      is_connected = 1;
      Serial.print("Connected to server ");
      Serial.println(client.remoteIP());
      // Формирование HTTP запроса:
      client.println("GET /search?q=arduino HTTP/1.1");
      client.println("Host: www.google.com");
      client.println("Connection: close");
      client.println();
    } else {
      // if you didn't get a connection to the server:
      Serial.println("Сonnection to server failed");
    }
  }
}

void loop() {
  // Периодические попытки установить соединение до успеха
  if (!is_connected) {
    delay(60000);
    setup();
  }
  // При успешном соединении вывод ответа от сервера
  else {
    int len = client.available();
    if (len > 0) {
      byte buffer[80];
      if (len > 80) len = 80;
      client.read(buffer, len);
      Serial.write(buffer, len);
    }
  }
}


